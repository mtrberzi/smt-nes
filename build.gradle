apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'jacoco'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  mavenCentral()
}

sourceSets {
  intTest {
    java.srcDir file('src/intTest/java')
    compileClasspath += sourceSets.test.compileClasspath
    runtimeClasspath += sourceSets.test.runtimeClasspath
  }
}

dependencies {
  compile group: "org.apache.commons", name: "commons-lang3", version:"3.3.+"
  testCompile 'junit:junit:4.12'
  intTestCompile 'junit:junit:4.12'
}

task intTest(type: Test) {
  testClassesDir = sourceSets.intTest.output.classesDir
  classpath = sourceSets.intTest.runtimeClasspath
}

jacoco {
  toolVersion = "0.7.4.201502262128"
  reportsDir = file("$buildDir/reports/jacoco")
}

test {
  jacoco {
    append = false
    destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
    classDumpFile = file("$buildDir/jacoco/classpathdumps")
  }
}

jacocoTestReport {
  reports {
    xml.enabled = true
    csv.enabled = false
    html.destination = "${buildDir}/reports/jacoco"
  }
}

check.dependsOn(jacocoTestReport)

